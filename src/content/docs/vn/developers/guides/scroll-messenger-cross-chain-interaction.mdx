---
section: developers
date: Last Modified
title: "Tương tác chuỗi chéo của Scroll Messenger"
lang: "vn"
permalink: "developers/guides/scroll-messenger-cross-chain-interaction"
whatsnext: { "Kết nối ERC20 thông qua Cổng tùy chỉnh": "/developers/guides/bridge-erc20-through-the-custom-gateway" }
excerpt: "Trong ví dụ này, chúng tôi sẽ khởi chạy một hợp đồng thông minh giả trên mạng thử nghiệm Sepolia hoặc Scroll Sepolia và tương tác với nó từ chuỗi đối diện."
---

import Aside from "../../../../../components/Aside.astro"

Trong ví dụ này, chúng tôi sẽ khởi chạy một hợp đồng thông minh giả trên Sepolia hoặc Scroll và tương tác với nó từ chuỗi đối diện. Chúng tôi sẽ sử dụng `ScrollMessenger` được triển khai trên cả hai Sepolia và cuộn.

## Triển khai các hợp đồng

### Hợp đồng thông minh mục tiêu

Hãy bắt đầu bằng cách triển khai hợp đồng thông minh mục tiêu. Chúng tôi sẽ sử dụng hợp đồng Greeter cho việc này ví dụ, nhưng bạn có thể sử dụng bất kỳ hợp đồng nào khác. Triển khai nó tới Sepolia hoặc Scroll. Trên cuộn, L1 và L2 sử dụng cùng một API, vì vậy điều đó tùy thuộc vào bạn.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

// Hợp đồng Greeter này sẽ được tương tác thông qua ScrollMessenger qua cầu
contract Greeter {
  string public greeting = "Hello World!";

  // Hàm này sẽ được gọi bởi executeFunctionCrosschain trên the Operator Smart Contract
  function setGreeting(string memory greeting_) public {
    greeting = greeting_;
  }
}
```

Bây giờ chúng ta sẽ thực thi `setGreeting` theo cách xuyên chuỗi.

### Operator Smart Contract

Chuyển sang chuỗi khác và triển khai `GreeterOperator`. Vì vậy, nếu bạn đã triển khai hợp đồng `Greeter` trên L1, hãy triển khai `GreeterOperator` trên L2 hoặc ngược lại.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

// Giao diện Scroll Messenger giống nhau trên cả L1 và L2, cho phép gửi giao dịch xuyên chuỗi
// Hãy import nó trực tiếp từ thư viện Hợp đồng Scroll
import "@scroll-tech/contracts@0.1.0/libraries/IScrollMessenger.sol";

// GreeterOperator có khả năng thực thi chức năng Greeter thông qua bridge
contract GreeterOperator {
  // Hàm này sẽ thực thi setGreeting trên hợp đồng Greeter
  function executeFunctionCrosschain(
    address scrollMessengerAddress,
    address targetAddress,
    uint256 value,
    string memory greeting,
    uint32 gasLimit
  ) public payable {
    IScrollMessenger scrollMessenger = IScrollMessenger(scrollMessengerAddress);
    // sendMessage is able to execute any function by encoding the abi using the encodeWithSignature function
    scrollMessenger.sendMessage{ value: msg.value }(
      targetAddress,
      value,
      abi.encodeWithSignature("setGreeting(string)", greeting),
      gasLimit,
      msg.sender
    );
  }
}
```

## Gọi hàm chuỗi chéo

Chúng tôi chuyển thông báo bằng cách thực thi `executeFunctionCrosschain` và chuyển các tham số sau:

- `scrollMessengerAddress`: Điều này sẽ phụ thuộc vào nơi bạn triển khai hợp đồng `GreeterOperator`.
  - Nếu bạn đã triển khai nó trên Sepolia, hãy sử dụng `0x50c7d3e7f7c656493D1D76aaa1a836CedfCBB16A`. Nếu bạn đã triển khai trên Scroll Sepolia, hãy sử dụng `0xBa50f5340FB9F3Bd074bD638c9BE13eCB36E603d`.
-`targetAddress`: Địa chỉ của hợp đồng `Greeter` trên chuỗi đối diện.
- `value`: Trong trường hợp này, nó là `0` vì `setGreeting`không được thanh toán.
- `greeting`: Đây là tham số sẽ được gửi qua tin nhắn. Hãy thử chuyển `“This message was cross-chain!”`
- `gasLimit`:
  - Nếu bạn đang gửi tin nhắn từ L1 đến L2, giới hạn gas khoảng `1000000` là quá đủ. Điều đó có nghĩa là, nếu bạn đặt giá trị này quá cao và `msg.value` không bao gồm `gasLimit` * `baseFee`, giao dịch sẽ hoàn nguyên. Nếu `msg.value` lớn hơn phí gas, phần chưa sử dụng sẽ được hoàn lại.
  - Nếu bạn đang gửi tin nhắn từ L2 đến L1, hãy chuyển `0`, vì giao dịch sẽ được hoàn thành bằng cách thực hiện một giao dịch bổ sung trên L1.

### Chuyển tiếp tin nhắn khi gửi từ L2 đến L1

Khi một giao dịch được chuyển từ L2 sang L1, một thực hiện giao dịch rút tiền "execute withdrawal transaction" phải được gửi trên L1. Để làm điều này, bạn phải gọi `relayMessageWithProof` trên Messenger Scroll L1 hợp đồng từ ví EOA.

Bạn có thể thực hiện việc này trực tiếp trên [Etherscan Sepolia](https://sepolia.etherscan.io/address/0x50c7d3e7f7c656493d1d76aaa1a836cedfcbb16a#writeProxyContract#F3). Để làm như vậy, bạn sẽ cần phải chuyển bằng chứng bao gồm Merkle cho giao dịch bắc cầu và các thông số khác. Bạn sẽ truy vấn những thứ này bằng API Scroll Bridge.

{/* VIỆC CẦN LÀM: hoàn tất việc xem xét các vấn đề về API */}

Chúng tôi đang hoàn thiện các thông tin cụ thể về API nhưng hiện tại, hãy tìm nạp hoặc thu gọn điểm cuối sau:
```bash
curl "https://sepolia-api-bridge.scroll.io/api/claimable?page_size=10&page=1&address=GREETER_OPERATOR_ADDRESS_ON_L2"
```

Thay thế `GREETER_OPERATOR_ADDRESS_ON_L2` bằng địa chỉ hợp đồng GreeterOperator của bạn như đã ra mắt trên L2. Đọc thêm về Thực hiện giao dịch Rút tiền trong bài viết [Scroll Messenger](/developers/l1-and-l2-bridge/the-scroll-messenger).

<Aside type="danger" title="Experimental API">
 API này được tạo cho Bridge UI của chúng tôi. Nó vẫn chưa được hoàn thiện và có thể thay đổi trong tương lai. Chúng tôi sẽ cập nhật hướng dẫn này khi API được hoàn thiện.
</Aside>

<Aside type="caution" title="Anyone can execute your L2 → L1 Message">
  `relayMessageWithProof` hoàn toàn không được phép, vì vậy bất kỳ ai cũng có thể gọi nó thay mặt bạn nếu họ sẵn sàng trả L1 phí gas. Tính năng này cho phép cơ sở hạ tầng hỗ trợ bổ sung, bao gồm công cụ để tự động hóa quy trình này cho ứng dụng và người dùng.
</Aside>

Sau khi thực hiện và xác nhận giao dịch trên cả L1 và L2, trạng thái `greeting` mới trên hợp đồng `Greeter` sẽ là `“This message was cross-chain!”`. Việc gửi tin nhắn từ chuỗi này sang chuỗi khác sẽ mất khoảng 20 phút sau khi giao dịch được xác nhận trên chuỗi gốc.

Xin chúc mừng, giờ đây bạn đã thực hiện giao dịch từ chuỗi này sang chuỗi khác bằng cách sử dụng cầu nối gốc của chúng tôi!
