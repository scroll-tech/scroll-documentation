---
section: developers
date: Last Modified
title: "Bridge ERC20 menggunakan Gateway Kustom"
lang: "id"
permalink: "developers/guides/bridge-erc20-through-the-custom-gateway"
excerpt: "Panduan ini akan menjelaskan cara menggunakan bridge Scroll untuk ERC20 yang memerlukan fungsionalitas khusus menggunakan Gateway Kustom."
---

import Aside from "../../../../../components/Aside.astro"

Panduan ini akan menjelaskan cara menggunakan bridge Scroll untuk ERC20 yang memerlukan fungsionalitas khusus menggunakan Gateway Kustom.

<Aside type="tip" title="Apakah Anda memerlukan logika khusus?">
  Ingat, dalam banyak konteks, Anda tidak perlu menggunakan Gateway Kustom untuk menambahkan fitur tambahan 
  ke token Anda atau ke logika penghubungnya. Lihat artikel di [ETH and ERC20 Token
  Bridge](/id/developers/l1-and-l2-bridging/eth-and-erc20-token-bridge#deposit-eth-and-erc20-tokens-from-l1) untuk informasi lebih lanjut.
</Aside>

## Langkah 1: Meluncurkan token di Sepolia

Pertama, kita membutuhkan sebuah token untuk di bridge. Tidak perlu adanya implementasi ERC20 tertentu agar token kompatibel dengan L2. Jika Anda sudah memiliki sebuah token, silakan lewati langkah ini. Jika Anda ingin mendeploy token baru, gunakan kontrak berikut dari token ERC20 sederhana yang membuat 1 juta token untuk pengirim ketika diluncurkan.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract L1Token is ERC20 {
  constructor() ERC20("My Token L1", "MTL1") {
    _mint(msg.sender, 1_000_000 ether);
  }
}
```

## Langkah 2: Meluncurkan token pasangan di Scroll Sepolia testnet

Selanjutnya, Anda akan meluncurkan sebuah token pasangan untuk token ini di Scroll, yang akan mewakili token asli di Sepolia. Token ini dapat mengimplementasikan logika kustom untuk mencocokkan token L1 atau bahkan menambahkan fitur tambahan di luar token L1.

Agar ini berhasil:

- Token harus mengimplementasikan antarmuka `IScrollStandardERC20` agar kompatibel dengan bridge.
- Kontrak harus menyediakan alamat gateway dan alamat token pasangan (token L1 yang baru saja diluncurkan) di bawah fungsi `gateway()` dan `counterpart()`. Ini juga harus mengizinkan gateway L2 untuk memanggil fungsi `mint()` dan `burn()` token, yang dipanggil saat token disetor dan ditarik.

Berikut adalah contoh lengkap dari sebuah token yang kompatibel dengan bridge. Pada konstruktor, Anda akan melewati alamat resmi Gateway Kustom Scroll (`0x058dec71E53079F9ED053F3a0bBca877F6f3eAcf`) dan alamat token yang diluncurkan di Sepolia.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@scroll-tech/contracts@0.1.0/libraries/token/IScrollERC20Extension.sol";

contract L2Token is ERC20, IScrollERC20Extension {
  // We store the gateway and the L1 token address to provide the gateway() and counterpart() functions which are needed from the Scroll Standard ERC20 interface
  address _gateway;
  address _counterpart;

  // In the constructor we pass as parameter the Custom L2 Gateway and the L1 token address as parameters
  constructor(address gateway_, address counterpart_) ERC20("My Token L2", "MTL2") {
    _gateway = gateway_;
    _counterpart = counterpart_;
  }

  function gateway() public view returns (address) {
    return _gateway;
  }

  function counterpart() external view returns (address) {
    return _counterpart;
  }

  // We allow minting only to the Gateway so it can mint new tokens when bridged from L1
  function transferAndCall(address receiver, uint256 amount, bytes calldata data) external returns (bool success) {
    transfer(receiver, amount);
    data;
    return true;
  }

  // We allow minting only to the Gateway so it can mint new tokens when bridged from L1
  function mint(address _to, uint256 _amount) external onlyGateway {
    _mint(_to, _amount);
  }

  // Similarly to minting, the Gateway is able to burn tokens when bridged from L2 to L1
  function burn(address _from, uint256 _amount) external onlyGateway {
    _burn(_from, _amount);
  }

  modifier onlyGateway() {
    require(gateway() == _msgSender(), "Ownable: caller is not the gateway");
    _;
  }
}
```

## Langkah 3: Tambahkan token ke Bridge Scroll

Anda perlu menghubungi tim Scroll untuk menambahkan token ke kontrak `L2CustomERC20Gateway` di Scroll dan kontrak `L1CustomERC20Gateway` di L1. Selain itu, ikuti petunjuk di repositori [daftar token](https://github.com/scroll-tech/token-list) untuk menambahkan token Anda ke frontend Bridge resmi Scroll.

## Langkah 4: Setor token

Setelah token Anda disetujui oleh tim Scroll, Anda seharusnya dapat menyetor token dari L1. Untuk melakukannya, Anda harus pertama-tama menyetujui alamat kontrak `L1CustomGateway` di Sepolia (`0x31C994F2017E71b82fd4D8118F140c81215bbb37`). Kemudian, setorkan token dengan memanggil fungsi `depositERC20` dari kontrak `L1CustomGateway`. Ini dapat dilakukan menggunakan [antarmuka Bridge kami](https://scroll.io/bridge), [Etherscan Sepolia](https://sepolia.etherscan.io/address/0x31C994F2017E71b82fd4D8118F140c81215bbb37#writeProxyContract), atau Smart Contract.

## Langkah 5: Tarik token

Anda akan mengikuti langkah-langkah serupa untuk mengirim token kembali dari L2 ke L1. Pertama, setujui alamat `L2CustomGateway` (`0x058dec71E53079F9ED053F3a0bBca877F6f3eAcf`) dan kemudian tarik token dengan memanggil `withdrawERC20` dari kontrak `L2CustomGateway`.

## Pendekatan Alternatif: Meluncurkan dan mengatur kontrak gateway L1 kustom

Menambahkan token Anda ke Bridge resmi Scroll (seperti yang dijelaskan di atas) adalah metode yang direkomendasikan untuk menghubungkan token ke dan dari Scroll. Pendekatan ini akan membuatnya lebih mudah ditemukan dan lebih aman bagi pemegangnya. Namun, ini akan memerlukan persetujuan dari tim Scroll. Jika Anda ingin meluncurkan token kustom tanpa proses persetujuan resmi, Anda dapat meluncurkan gateway kustom sendiri. Untuk melakukannya, Anda perlu mendeploy kontrak `L1CustomERC20Gateway` di L1 dan `L2CustomERC20Gateway` di L2.

### Meluncurkan Gateway Kustom L1

Mari mulai dengan meluncurkan kontrak berikut di Sepolia.

```solidity
// SPDX-License-Identifier: MIT

// Although it's possible to use other Solidity versions, we recommend using version 0.8.16 because that's where our contracts were audited
pragma solidity =0.8.16;

import "@openzeppelin/contracts/access/Ownable.sol";

import { IL2ERC20Gateway } from "@scroll-tech/contracts@0.1.0/L2/gateways/IL2ERC20Gateway.sol";
import { IL1ScrollMessenger } from "@scroll-tech/contracts@0.1.0/L1/IL1ScrollMessenger.sol";
import { IL1ERC20Gateway } from "@scroll-tech/contracts@0.1.0/L1/gateways/IL1ERC20Gateway.sol";

import { ScrollGatewayBase } from "@scroll-tech/contracts@0.1.0/libraries/gateway/ScrollGatewayBase.sol";
import { L1ERC20Gateway } from "@scroll-tech/contracts@0.1.0/L1/gateways/L1ERC20Gateway.sol";

// This contract will be used to send and receive tokens from L2
contract L1CustomERC20Gateway is L1ERC20Gateway, Ownable {
  // Tokens must be mapped to "bind" them to a token that represents the original token on the original. This event will be emitted when the token mapping for ERC20 token is updated.
  event UpdateTokenMapping(address indexed l1Token, address indexed oldL2Token, address indexed newL2Token);

  mapping(address => address) public tokenMapping;

  constructor() {}

  // This function must be called once after both the L1 and L2 contract was deployed
  function initialize(address _counterpart, address _router, address _messenger) external {
    require(_router != address(0), "zero router address");

    ScrollGatewayBase._initialize(_counterpart, _router, _messenger);
  }

  /// This function returns the address of the token on L2
  function getL2ERC20Address(address _l1Token) public view override returns (address) {
    return tokenMapping[_l1Token];
  }

  // Updates the token mapping that "binds" a token with another one on the other chain
  function updateTokenMapping(address _l1Token, address _l2Token) external onlyOwner {
    require(_l2Token != address(0), "token address cannot be 0");

    address _oldL2Token = tokenMapping[_l1Token];
    tokenMapping[_l1Token] = _l2Token;

    emit UpdateTokenMapping(_l1Token, _oldL2Token, _l2Token);
  }

  // Callback called before a token is withdrawn on L1
  function _beforeFinalizeWithdrawERC20(
    address _l1Token,
    address _l2Token,
    address,
    address,
    uint256,
    bytes calldata
  ) internal virtual override {
    require(msg.value == 0, "nonzero msg.value");
    require(_l2Token != address(0), "token address cannot be 0");
    require(_l2Token == tokenMapping[_l1Token], "l2 token mismatch");
  }

  // Token bridged can be "canceled" or dropped. This callback is called before that happens.
  function _beforeDropMessage(address, address, uint256) internal virtual override {
    require(msg.value == 0, "nonzero msg.value");
  }

  // Internal function holding the deposit logic
  function _deposit(
    address _token,
    address _to,
    uint256 _amount,
    bytes memory _data,
    uint256 _gasLimit
  ) internal virtual override nonReentrant {
    address _l2Token = tokenMapping[_token];
    require(_l2Token != address(0), "no corresponding l2 token");

    // 1. Transfer token into this contract.
    address _from;
    (_from, _amount, _data) = _transferERC20In(_token, _amount, _data);

    // 2. Generate message passed to L2CustomERC20Gateway.
    bytes memory _message = abi.encodeCall(
      IL2ERC20Gateway.finalizeDepositERC20,
      (_token, _l2Token, _from, _to, _amount, _data)
    );

    // 3. Send message to L1ScrollMessenger.
    IL1ScrollMessenger(messenger).sendMessage{ value: msg.value }(counterpart, 0, _message, _gasLimit, _from);

    emit DepositERC20(_token, _l2Token, _from, _to, _amount, _data);
  }
}
```

### Meluncurkan Gateway Kustom L2

Sekarang mari kita meluncurkan kontrak pasangan di Scroll.

```solidity
// SPDX-License-Identifier: MIT

pragma solidity =0.8.16;

import "@openzeppelin/contracts/access/Ownable.sol";

import "@scroll-tech/contracts@0.1.0/L2/gateways/L2ERC20Gateway.sol";
import { IL2ScrollMessenger } from "@scroll-tech/contracts@0.1.0/L2/IL2ScrollMessenger.sol";
import { IL1ERC20Gateway } from "@scroll-tech/contracts@0.1.0/L1/gateways/IL1ERC20Gateway.sol";
import { ScrollGatewayBase } from "@scroll-tech/contracts@0.1.0/libraries/gateway/ScrollGatewayBase.sol";
import "@scroll-tech/contracts@0.1.0/libraries/token/IScrollERC20Extension.sol";

import { IL2ERC20Gateway } from "@scroll-tech/contracts@0.1.0/L2/gateways/IL2ERC20Gateway.sol";

// This contract will be used to send and receive tokens from L1
contract L2CustomERC20Gateway is L2ERC20Gateway, ScrollGatewayBase, Ownable {
  event UpdateTokenMapping(address indexed l2Token, address indexed oldL1Token, address indexed newL1Token);

  // solhint-disable-next-line var-name-mixedcase
  mapping(address => address) public tokenMapping;

  constructor() {}

  // Like with the L1 version of the Gateway, this must be called once after both the L1 and L2 gateways are deployed
  function initialize(address _counterpart, address _router, address _messenger) external {
    require(_router != address(0), "zero router address");

    ScrollGatewayBase._initialize(_counterpart, _router, _messenger);
  }

  /// Returns the address of the token representing the token on L2
  function getL1ERC20Address(address _l2Token) external view override returns (address) {
    return tokenMapping[_l2Token];
  }

  // This returns the L2 token address
  function getL2ERC20Address(address) public pure override returns (address) {
    revert("unimplemented");
  }

  // This function finalizes the token deposit on L2 when the deposit was not finalized due to not enough gas sent from L1
  function finalizeDepositERC20(
    address _l1Token,
    address _l2Token,
    address _from,
    address _to,
    uint256 _amount,
    bytes calldata _data
  ) external payable override onlyCallByCounterpart nonReentrant {
    require(msg.value == 0, "nonzero msg.value");
    require(_l1Token != address(0), "token address cannot be 0");
    require(_l1Token == tokenMapping[_l2Token], "l1 token mismatch");

    IScrollERC20Extension(_l2Token).mint(_to, _amount);

    _doCallback(_to, _data);

    emit FinalizeDepositERC20(_l1Token, _l2Token, _from, _to, _amount, _data);
  }

  // Same as in the L1 version of this contract, this function "binds" a token with a token on the other chain
  function updateTokenMapping(address _l2Token, address _l1Token) external onlyOwner {
    require(_l1Token != address(0), "token address cannot be 0");

    address _oldL1Token = tokenMapping[_l2Token];
    tokenMapping[_l2Token] = _l1Token;

    emit UpdateTokenMapping(_l2Token, _oldL1Token, _l1Token);
  }

  // Internal function holding the withdraw logic
  function _withdraw(
    address _token,
    address _to,
    uint256 _amount,
    bytes memory _data,
    uint256 _gasLimit
  ) internal virtual override nonReentrant {
    address _l1Token = tokenMapping[_token];
    require(_l1Token != address(0), "no corresponding l1 token");

    require(_amount > 0, "withdraw zero amount");

    // 1. Extract real sender if this call is from L2GatewayRouter.
    address _from = msg.sender;
    if (router == msg.sender) {
      (_from, _data) = abi.decode(_data, (address, bytes));
    }

    // 2. Burn token.
    IScrollERC20Extension(_token).burn(_from, _amount);

    // 3. Generate message passed to L1StandardERC20Gateway.
    bytes memory _message = abi.encodeCall(
      IL1ERC20Gateway.finalizeWithdrawERC20,
      (_l1Token, _token, _from, _to, _amount, _data)
    );

    // 4. send message to L2ScrollMessenger
    IL2ScrollMessenger(messenger).sendMessage{ value: msg.value }(counterpart, 0, _message, _gasLimit);

    emit WithdrawERC20(_l1Token, _token, _from, _to, _amount, _data);
  }
}
```

### Menyiapkan kontrak Gateway Anda di Sepolia

Setelah kontrak-kontrak itu didaftarkan, panggil fungsi-fungsi berikut untuk menginisialisasi kontrak-kontrak tersebut dan mengaitkannya dengan token-tokn dan gateway di sisi lain Bridge.

Pertama, panggil fungsi `initialize` pada kontrak `MyL1Gateway` dengan parameter-parameter berikut:

- `_counterpart`: Alamat `MyL2Gateway` yang baru saja kita luncurkan di Scroll.
- `_router`: Tetapkan ke `0x13FBE0D0e5552b8c9c4AE9e2435F38f37355998a`, kontrak `L1GatewayRouter` di Sepolia.
- `_messenger`: Tetapkan ke `0x50c7d3e7f7c656493D1D76aaa1a836CedfCBB16A`, kontrak `L1ScrollMessenger` di Sepolia.

Sebuah gateway kustom dapat menampung beberapa Bridge token. Dalam kasus ini, kita hanya akan mengizinkan pembuatan Bridge antara L1Token dan L2Token dengan memanggil fungsi `updateTokenMapping` pada kontrak `MyL1Gateway` dengan parameter-parameter berikut:

- `_l1Token`: Alamat kontrak `L1Token` yang sebelumnya kita luncurkan di Sepolia.
- `_l2Token`: Alamat kontrak `L2Token` yang sebelumnya kita luncurkan di Scroll.

### Menyiapkan kontrak Gateway Anda di Scroll

Sekarang mari beralih ke jaringan Scroll dan inisialisasi `MyL2Gateway`, mengikuti langkah-langkah serupa.

Pertama, panggil fungsi `initialize` dari `MyL2Gateway`:

- `_counterpart`: Alamat `MyL1Gateway` yang baru saja kita luncurkan di Sepolia.
- `_router`: Tetapkan ke `0x9aD3c5617eCAa556d6E166787A97081907171230`, kontrak `L2GatewayRouter` di Scroll.
- `_messenger`: Tetapkan ke `0xBa50f5340FB9F3Bd074bD638c9BE13eCB36E603d`, kontrak `L2ScrollMessenger` di Scroll.

Selanjutnya, panggil `updateTokenMapping` pada kontrak `MyL2Gateway`:

- `_l2Token`: Alamat kontrak `L2Token` yang sebelumnya kita luncurkan di Scroll.
- `_l1Token`: Alamat kontrak `L1Token` yang sebelumnya kita luncurkan di Sepolia.

### Bridge token

Sekarang kita dapat memanggil `depositERC20` dari `MyL1Gateway` dan `withdrawERC20` dari `MyL2Gateway` sebagaimana halnya dengan Bridge resmi Scroll.
